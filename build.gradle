buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:0.9.17'
    }
}

allprojects {
    group = 'me.libs'
    version = '1-SNAPSHOT'
    apply plugin: 'idea'
    repositories {
        jcenter()
    }
}

project(':server') {
    repositories {
        maven { url 'http://clinker.netty.io/nexus/content/repositories/snapshots' }
    }

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'io.ratpack.ratpack-groovy'

    dependencies {
        compile ratpack.dependency('jackson')
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    }
}

project(':book-slicer') {
    apply plugin: 'java'
}

idea {
    project {
        //use JDK 1.8 in idea
        jdkName '1.8'
        languageLevel '1.8'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

task stage {
    dependsOn ':server:installDist'
}